on join:
    set {deaths::%player's uuid%} to 0 if {deaths::%player's uuid%} is not set
    set {rebirths::%player's uuid%} to 0 if {rebirths::%player's uuid%} is not set

function updateBoard():
    set {_sorted::*} to sorted indices of {deaths::*} in descending order
    loop 10 times:
        set {_p} to {_sorted::%loop-number%} parsed as offline player
        set {_v} to {deaths::%{_p}'s uuid%}

        console command "/holo setline leaderboard %(loop-value + 1)% &8%loop-value%. &f%{_p}% &8- &c%format({_v})%"

every minute in world "world":
    updateBoard()

on load:
    delete {yaml::*}
    delete {shop::mobs::*}
    wait 1 tick

    load all yaml from directory "plugins/Skript/scripts/povgrinder-git/mobs/"
    load all yaml from directory "plugins/Skript/scripts/povgrinder-git/mobs/"

    loop all the currently loaded yaml files:   
        set {_type} to yaml value "type" from loop-value
        add loop-value to {yaml::%{_type}%::*}
    
    loop {yaml::mob::*}:
        set {_index} to yaml value "index" from loop-value

        set {_name} to yaml value "name" from loop-value
        set {_display} to yaml value "item" from loop-value

        set {_deaths} to yaml value "deaths" from loop-value
        set {_blood_req} to (2^({_index}+1))*5
        set {_blood_boost} to ((2^({_index}+1))*5)/10
        add floor({_index}^1.5) to {_blood_req}
        set {_rebirths} to yaml value "rebirths" from loop-value

        set {_mob} to yaml value "mob" from loop-value

        set {_unlocked} to ({_display} parsed as itemtype) named {_name} with all flags hidden
        set {_locked} to iron bars named "&cLocked" with all flags hidden

        add "&8(&c%format({_blood_boost})% blood &8/ death&8)" to lore of {_locked}
        add "&8(&c%format({_blood_boost})% blood &8/ death&8)" to lore of {_unlocked}

        add "&6Click to equip." to lore of {_unlocked}
        
        if {_blood_req} != 0:
            add "&7You need &c&n%format({_blood_req})% blood&7 to unlock this." to lore of {_locked}
        
        if {_rebirths} != 0:
            add "&7You need &d&n%format({_rebirths})% rebirths&7 to unlock this." to lore of {_locked}
        
        set {shop::mobs::%{_index}%::unlocked} to {_unlocked}
        set {shop::mobs::%{_index}%::locked} to {_locked}
        set {shop::mobs::%{_index}%::mob} to {_mob}
        set {shop::mobs::%{_index}%::deaths} to {_blood_req}
        set {shop::mobs::%{_index}%::boost} to {_blood_boost}
        set {shop::mobs::%{_index}%::rebirths} to {_rebirths}
        set {shop::mobs::%{_index}%::name} to {_name}

        add 1 to {shop::mobs::total}

function gui_shop(p: player):
    set {_gui} to chest inventory with 3 rows named "Shop"
    set slots (integers between 0 and 26) of {_gui} to black stained glass pane named "&7"
    set slots (integers between 10 and 16) of {_gui} to light gray stained glass pane named "&7"

    # set slot 11 of {_gui} to zombie spawn egg named "&2Zombie" with lore "&6Click to equip."
    set slot 11 of {_gui} to zombie spawn egg named "&6Monsters" with all flags hidden
    set slot 13 of {_gui} to lime stained glass named "&6Auras" with all flags hidden
    set slot 15 of {_gui} to name tag named "&6Ranks" with all flags hidden

    set slot 22 of {_gui} to barrier named "&cClose" with all flags hidden

    set metadata tag "gui.shop" of {_p} to {_gui}
    open {_gui} to {_p}

on inventory click:
    if metadata tag "gui.shop" of player = event-inventory:
        cancel event
        if index of event-slot = 11:
            gui_mob_shop(player, 0)
        if index of event-slot = 13:
            send "&cThe Aura Shop is currently under development." to player
        if index of event-slot = 22:
            close player's inventory

function gui_mob_shop(p: player, i: integer):
    set {_gui} to chest inventory with 3 rows named "Shop - Monsters"
    set slots (integers between 0 and 26) of {_gui} to black stained glass pane named "&7"
    set slots (integers between 9 and 17) of {_gui} to light gray stained glass pane named "&7"

    loop 9 times:
        set {_index} to loop-value + ({_i} * 9) - 1
        if {_index} >= {shop::mobs::total}:
            stop loop
        
        if {deaths::%{_p}'s uuid%} >= {shop::mobs::%{_index}%::deaths}:
            if {rebirths::%{_p}'s uuid%} >= {shop::mobs::%{_index}%::rebirths}:
                set {_item} to {shop::mobs::%{_index}%::unlocked}
            else:
                set {_item} to {shop::mobs::%{_index}%::locked}
        else:
            set {_item} to {shop::mobs::%{_index}%::locked}
        
        set slot (loop-value + 9 - 1) of {_gui} to {_item}
    
    if {_i} > 0:
        set slot 18 of {_gui} to arrow named "&cPrevious" with all flags hidden
    set slot 22 of {_gui} to oak door named "&6Back" with all flags hidden

    if {_i} < floor(({shop::mobs::total}-1) / 9):
        set slot 26 of {_gui} to arrow named "&aNext" with all flags hidden
    
    set metadata tag "gui.shop.mobs" of {_p} to {_gui}
    set metadata tag "page.shop.mobs" of {_p} to "%{_i}%"
    open {_gui} to {_p}
        
on inventory click:
    metadata tag "gui.shop.mobs" of player = event-inventory
    cancel event
    set {_page} to ((metadata tag "page.shop.mobs" of player) parsed as integer ? 0)

    if index of event-slot = 18:
        if {_page} > 0:
            remove 1 from {_page}
            gui_mob_shop(player, {_page})
            stop

    if index of event-slot = 26:
        if {_page} < floor(({shop::mobs::total}-1) / 9):
            add 1 to {_page}
            gui_mob_shop(player, {_page})
            stop
    
    if index of event-slot = 22:
        gui_shop(player)
        stop
    
    if index of event-slot < 9:
        stop
    if index of event-slot > 17:
        stop
    
    loop integers between 9 and 17:
        if index of event-slot = loop-value:
            set {_index} to (loop-value - 9) + ({_page} * 9)
            if {_index} >= {shop::mobs::total}:
                stop
            
            if {deaths::%player's uuid%} >= {shop::mobs::%{_index}%::deaths}:
                if {rebirths::%player's uuid%} >= {shop::mobs::%{_index}%::rebirths}:
                    set {entity::%player's uuid%} to {shop::mobs::%{_index}%::mob} parsed as entitytype
                    set {boost::%player's uuid%} to {shop::mobs::%{_index}%::boost}
                    send "&fYou have equipped the %{shop::mobs::%{_index}%::name}% &fmob." to player
                else:
                    send "&cYou have not unlocked this yet." to player
            else:
                send "&cYou have not unlocked this yet." to player
                
            
            close player's inventory
            stop



# function gui_aura_shop

# we have a working paginated mob shop.
# now, to unlock aruas we'll use lootboxes.
# player's right click their lootboxes to open them.
# leader boards

command /shop:
    trigger:
        if player's y coord < 120:
            player command "/spawn"
        gui_shop(player)

command /rebirth:
    trigger:
        if {deaths::%player's uuid%} < 10000:
            send "&cYou need at least %format(10000)% blood to rebirth." to player
            stop
        
        
        set {deaths::%player's uuid%} to 10
        delete {entity::%player's uuid%}
        delete {boost::%player's uuid%}

        add 1 to {multi::%player's uuid%}

        add 1 to {rebirths::%player's uuid%}
        send "&dYou have rebirthed! You now have %format({rebirths::%player's uuid%})% rebirths." to player
        send "&d%player% has just rebirthed!" to all players